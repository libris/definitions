prefix madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

construct {

    ?s ?p ?o ;
        rdfs:label ?lcLabel ;
        skos:prefLabel ?rdapreflabel ;
        skos:definition ?rdadefinition .

} where {
    {
        graph <https://id.kb.se/dataset/rdaterms> {
            ?s ?p ?o .
        }
    } union {
        ?s skos:exactMatch ?lc .
        filter(strstarts(str(?lc), 'http://id.loc.gov/vocabulary/'))
        ?lc madsrdf:authoritativeLabel|skos:prefLabel ?lcLabel .
    } union {
        ?s skos:exactMatch ?rda .
        filter(strstarts(str(?rda), 'http://rdaregistry.info/termList/'))
        ?rda skos:prefLabel ?rdapreflabel ;
            skos:definition ?rdadefinition .
    }
}
