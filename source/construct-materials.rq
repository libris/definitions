prefix skos: <http://www.w3.org/2004/02/skos/core#>

construct {
    ?s ?p ?o ;
        skos:prefLabel ?prefLabel ;
        skos:definition ?definition .
} where {
    graph <https://id.kb.se/dataset/materials> {
        {
            ?s ?p ?o .
        } union {
            ?s skos:exactMatch ?aat .
            graph <sparql/aat-materials> {
                ?aat skos:prefLabel ?aatLabel .
            }
            optional {
                ?s skos:exactMatch ?rda .
                graph <http://rdaregistry.info/termList/RDAMaterial.nt> {
                    ?rda skos:prefLabel ?rdaLabel ;
                        skos:definition ?definition .
                }
            }
            bind(coalesce(?rdaLabel, ?aatLabel) as ?prefLabel)
        }
    }
}