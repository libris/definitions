prefix : <http://schema.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix dc: <http://purl.org/dc/terms/>

construct {
    ?s a :Country;
        skos:exactMatch ?loc;
        skos:exactMatch ?match;
        dc:isReplacedBy ?replacement;
        skos:notation ?notation;
        rdfs:comment ?comment;
        skos:prefLabel ?label;
        skos:prefLabel ?prefLabel .

} where {
    graph <https://id.kb.se/dataset/countries> {
        {
            ?s skos:notation ?notation ; skos:prefLabel ?label .
            optional { ?s rdfs:comment ?comment }
            optional { ?s skos:exactMatch ?match }
            optional {
                graph <http://id.loc.gov/vocabulary/countries> {
                    ?loc skos:prefLabel ?prefLabel .
                    filter strends(str(?loc), concat('/', ?notation))
                }
            }
        } union {
            ?s dc:isReplacedBy ?replnot .
            ?replacement skos:notation ?replnot .
        }
    }
}
