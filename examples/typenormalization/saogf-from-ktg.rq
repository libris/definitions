# - Do the same for barngf,rdacategories ?
# - No genreform:s for instances (any left in saogf or tgm)! See values in materialformat,carrierforms ...

prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix saogf: <https://id.kb.se/term/saogf/>
prefix ktg: <https://id.kb.se/term/ktg/>
prefix : <https://id.kb.se/vocab/>

construct {
  ?saogf_s ?target_p ?target_o .
  ?unmapped_s :editorialNote "UNMAPPED" .
} {
  ?s ?p ?o .
  bind( if( strstarts(str(?p), str(skos:)),
            IRI(concat(str(:), strafter(str(?p), str(skos:)))),
            ?p) as ?target_p )
  optional {
    ?s skos:exactMatch ?saogf_s .
    filter strstarts(str(?s), str(ktg:))
    # TODO: to/from other schemes (barngf, tgm) as well?
    filter strstarts(str(?saogf_s), str(saogf:))
  }
  bind(if(!bound(?saogf_s), ?s, ?_) as ?unmapped_s)  # "Should Not Happen(TM)"

  optional {
    ?o skos:exactMatch ?saogf_o .
    filter strstarts(str(?o), str(ktg:))
    filter strstarts(str(?saogf_o), str(saogf:))
  }
  bind(coalesce(?saogf_o, ?o) as ?target_o)
  filter(?target_o != ?saogf_s)
}
